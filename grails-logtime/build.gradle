buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id "org.sonarqube" version "2.5"
}

// inherit from parent project version
//version "0.3"
group "grails.plugin.logtime"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin: 'org.sonarqube'
apply plugin: "jacoco"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    console "org.grails:grails-console"
    profile "org.grails.profiles:plugin:3.1.4"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.grails:grails-plugin-testing"
    ////
    compile "org.grails:grails-dependencies"  // to include aspectj libs
    compile "org.springframework.boot:spring-boot-starter-tomcat"  // needed by grails-dependencies
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

test {
    maxParallelForks = Runtime.runtime.availableProcessors()
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}
jacoco {
    toolVersion = "0.7.9"
}
jacocoTestReport {
    reports {
        xml.enabled true
    }
    executionData test
}

/**
 ./gradlew sonarqube \
 -Dsonar.host.url=http://localhost:9000 \
 -Dsonar.login=6251af147a77913f1977d7e40aea263f5726f95e
 */
sonarqube {
    properties {
        property "sonar.projectName", "grails-logtime"
        property "sonar.groovy.binaries", "${project.buildDir.name}${File.separator}classes/main"
        property 'sonar.groovy.jacoco.itReportPath', "$buildDir/jacoco/integrationTest.exec"
        property "sonar.groovy.jacoco.reportPath", "$buildDir/jacoco/test.exec"
    }
}


// enable if you wish to package this plugin as a standalone application
bootRepackage.enabled = false
grailsPublish {
    // load bintray user and key from jvm -Dvar for security
    user = System.getProperty('bintrayUser')
    key = System.getProperty('bintrayKey')
    githubSlug = 'ikaliZpet/grails-appinfo'
    license {
        name = 'Apache-2.0'
    }
    title = "Grails Logtime plugin"
    desc = "A Grails plugin to provide method time tracking and execution aspect logging"
    developers = [binle: "Bin Le"]
//    portalUser = ""
//    portalPassword = ""

    // with above configuration, run './gradlew bintrayUpload' command as:
    //   bintrayUpload -DbintrayUser=ikalizpet  -DbintrayKey=f3e86cc1d...(API key given by bintray)
}
